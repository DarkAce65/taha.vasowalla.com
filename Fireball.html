<!DOCTYPE html>
<html>
	<head>
		<title>Fireball</title>
		<link rel="shortcut icon" href="img/icons/TV.png"/>
		<meta charset="utf-8">
		<meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1">
		<meta name="viewport" content="width=device-width, initial-scale=1">
		<meta name="theme-color" content="#3d3d3d">
		<meta name="msapplication-navbutton-color" content="#3d3d3d">
		<link rel="stylesheet" type="text/css" href="bower_components/bootstrap/dist/css/bootstrap.min.css"/>
		<link rel="stylesheet" type="text/css" href="bower_components/font-awesome/css/font-awesome.min.css"/>
		<link rel="stylesheet" type="text/css" href="bower_components/toastr/toastr.min.css"/>
		<script src="bower_components/jquery/dist/jquery.min.js"></script>
		<script src="bower_components/bootstrap/dist/js/bootstrap.min.js"></script>
		<script src="bower_components/three.js/build/three.js"></script>
		<script src="lib/GSAP/TweenLite.min.js"></script>
		<script src="lib/GSAP/easing/EasePack.min.js"></script>
		<script src="lib/THREE.Controls/TrackballControls.js"></script>
		<style type="text/css">
			html, body {
				width: 100%;
				height: 100%;
				margin: 0;
				padding: 0;
				overflow: hidden;
			}

			#controls {
				position: fixed;
				top: 5px;
				right: 5px;
				text-align: right;
			}

			.dropdown-menu > li > a, .dropdown-menu {
				background-color: #d9534f;
				color: white;
			}

			.dropdown-menu > li > a:focus, .dropdown-menu > li > a:hover {
				background-color: #c9302c;
				color: white;
				text-decoration: none;
			}
		</style>
	</head>
	<body>
		<div id="controls">
			<div class="btn-group">
				<button type="button" class="btn btn-danger dropdown-toggle" data-toggle="dropdown">
					<i class="fa fa-fw fa-video-camera"></i> Camera <span class="caret"></span>
				</button>
				<ul class="dropdown-menu pull-right">
					<li><a href="javascript:void(0)" onclick="setCamera('Reset')">Reset</a></li>
				</ul>
			</div>
		</div>
		<div id="rendererContainer"></div>
		<script id="vertexShader" type="x-shader/x-vertex">
			uniform float u_time;

			varying vec2 vUv;
			varying float noise;

			// Credit for Perlin noise function goes to https://github.com/ashima/webgl-noise
			vec3 mod289(vec3 x) {
				return x - floor(x / 289.0) * 289.0;
			}

			vec4 mod289(vec4 x) {
				return x - floor(x / 289.0) * 289.0;
			}

			vec4 permute(vec4 x) {
				return mod289((x * 34.0 + 1.0) * x);
			}

			vec4 taylorInvSqrt(vec4 r) {
				return 1.79284291400159 - 0.85373472095314 * r;
			}

			vec3 fade(vec3 t) {
				return t * t * t * (t * (t * 6.0 - 15.0) + 10.0);
			}

			float pnoise(vec3 P, vec3 rep) {
				vec3 Pi0 = mod(floor(P), rep); // Integer part, modulo period
				vec3 Pi1 = mod(Pi0 + vec3(1.0), rep); // Integer part + 1, mod period
				Pi0 = mod289(Pi0);
				Pi1 = mod289(Pi1);
				vec3 Pf0 = fract(P); // Fractional part for interpolation
				vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
				vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
				vec4 iy = vec4(Pi0.yy, Pi1.yy);
				vec4 iz0 = Pi0.zzzz;
				vec4 iz1 = Pi1.zzzz;

				vec4 ixy = permute(permute(ix) + iy);
				vec4 ixy0 = permute(ixy + iz0);
				vec4 ixy1 = permute(ixy + iz1);

				vec4 gx0 = ixy0 * (1.0 / 7.0);
				vec4 gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
				gx0 = fract(gx0);
				vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
				vec4 sz0 = step(gz0, vec4(0.0));
				gx0 -= sz0 * (step(0.0, gx0) - 0.5);
				gy0 -= sz0 * (step(0.0, gy0) - 0.5);

				vec4 gx1 = ixy1 * (1.0 / 7.0);
				vec4 gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
				gx1 = fract(gx1);
				vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
				vec4 sz1 = step(gz1, vec4(0.0));
				gx1 -= sz1 * (step(0.0, gx1) - 0.5);
				gy1 -= sz1 * (step(0.0, gy1) - 0.5);

				vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
				vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
				vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
				vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
				vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
				vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
				vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
				vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);

				vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
				g000 *= norm0.x;
				g010 *= norm0.y;
				g100 *= norm0.z;
				g110 *= norm0.w;
				vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
				g001 *= norm1.x;
				g011 *= norm1.y;
				g101 *= norm1.z;
				g111 *= norm1.w;

				float n000 = dot(g000, Pf0);
				float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
				float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
				float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
				float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
				float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
				float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
				float n111 = dot(g111, Pf1);

				vec3 fade_xyz = fade(Pf0);
				vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
				vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
				float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x);
				return 2.2 * n_xyz;
			}

			float turbulence(vec3 p) {
				float w = 100.0;
				float t = -0.5;
				for(float f = 1.0; f <= 10.0; f++) {
					float power = pow(2.0, f);
					t += abs(pnoise(vec3(power * p), vec3(10.0, 10.0, 10.0)) / power);
				}
				return t;
			}

			void main() {
				vUv = uv;

				noise = -turbulence(0.5 * normal + vec3(u_time / 2.0));
				vec3 displacement = position + normal * (4.0 * pnoise(position * 0.05 + vec3(u_time), vec3(100.0)) + -10.0 * noise);
				vec4 mvPosition = modelViewMatrix * vec4(displacement, 1.0);
				gl_Position = projectionMatrix * mvPosition;
			}
		</script>
		<script id="fragmentShader" type="x-shader/x-fragment">
			#define PI 3.14159265359

			uniform float u_time;
			uniform sampler2D u_textureMap; // chroma.scale(["black", "maroon", "darkorange", "yellow", "white"]).domain([0, 0.1, 0.8, 0.9, 1]);

			varying vec2 vUv;
			varying float noise;

			float random(vec2 st) {
				return fract(sin(dot(st.xy, vec2(12.9898, 78.233))) * 43758.5453123);
			}

			void main() {
				vec2 texturePosition = vec2(0.0, noise + 0.08 + 0.02 * random(vUv));
				vec4 color = texture2D(u_textureMap, texturePosition);
				gl_FragColor = color;
			}
		</script>
		<script type="text/javascript">
			window.requestAnimFrame =
				window.requestAnimationFrame ||
				window.webkitRequestAnimationFrame ||
				window.mozRequestAnimationFrame ||
				window.oRequestAnimationFrame ||
				window.msRequestAnimationFrame ||
				function(callback) {
					window.setTimeout(callback, 1000 / 60);
				};

			var factor = window.innerWidth < 768 ? 1.5 : 1;

			var loader = new THREE.TextureLoader();
			var clock = new THREE.Clock();
			var scene = new THREE.Scene();
			var renderer = new THREE.WebGLRenderer({alpha: true, antialias: true});
			$("#rendererContainer").append(renderer.domElement);
			renderer.setSize(window.innerWidth, window.innerHeight);
			renderer.setClearColor(0x3d3d3d);
			renderer.setPixelRatio(window.devicePixelRatio);

			var camera = new THREE.PerspectiveCamera(35, window.innerWidth / window.innerHeight, 0.1, 10000);
			camera.position.set(0, 0, 100 * factor);
			scene.add(camera);
			var controls = new THREE.TrackballControls(camera, renderer.domElement);
			camera.lookAt(scene.position);

			var uniforms = {
				u_time: {type: "f", value: 0},
				u_textureMap: {type: "t", value: null}
			};
			var material = new THREE.ShaderMaterial({
				uniforms: uniforms,
				vertexShader: document.getElementById("vertexShader").textContent,
				fragmentShader: document.getElementById("fragmentShader").textContent,
				side: THREE.DoubleSide
			});
			var objectGeometry = new THREE.IcosahedronGeometry(20, 4);
			var object = new THREE.Mesh(objectGeometry, material);
			scene.add(object);

			function setCamera(position) {
				var x = 0, y = 0, z = 0,
					uy = 0;

				switch(position) {
					case "Reset":
					default:
						x = 0;
						y = 0;
						z = 100;
						uy = 1;
						break;
				}

				x *= factor;
				y *= factor;
				z *= factor;

				TweenLite.to(camera.position, 2, {x: x, y: y, z: z, ease: Power2.easeInOut});
				TweenLite.to(controls.target, 2, {x: 0, y: 0, z: 0, ease: Power2.easeOut, onUpdate: function(){camera.lookAt(controls.target);}});
				TweenLite.to(camera.up, 1, {x: 0, y: uy, z: 0});
			}

			function render() {
				requestAnimFrame(render);
				renderer.render(scene, camera);
				controls.update();

				var delta = clock.getDelta();
				uniforms.u_time.value += delta * 0.6;
			}

			loader.load("img/textures/fireball.png", function(texture) {
				uniforms.u_textureMap.value = texture;
			});
			render();

			$(window).resize(function() {
				renderer.setSize(window.innerWidth, window.innerHeight);
				camera.aspect = window.innerWidth / window.innerHeight;
				camera.updateProjectionMatrix();
				factor = window.innerWidth < 768 ? 1.5 : 1;
			});

			$(window).load(function() {
				renderer.setSize(window.innerWidth, window.innerHeight);
				camera.aspect = window.innerWidth / window.innerHeight;
				camera.updateProjectionMatrix();
			});
		</script>
	</body>
</html>